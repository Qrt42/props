import os

Import('env')

module_env = env.Clone()

if os.path.isdir('../mesh_data_resource'):
    module_env.Append(CPPDEFINES=['MESH_DATA_RESOURCE_PRESENT'])

sources = [

    "register_types.cpp",

    "props/prop_data.cpp",
    "props/prop_data_entry.cpp",
    "props/prop_data_scene.cpp",
    "props/prop_data_mesh.cpp",
    "props/prop_data_light.cpp",
    "props/prop_data_prop.cpp",
    "props/prop_data_entity.cpp",

    "clutter/ground_clutter.cpp",
    "clutter/ground_clutter_foliage.cpp",

    "prop_tool/prop_tool.cpp",
    "prop_tool/prop_tool_entity.cpp",
    "prop_tool/prop_tool_prop.cpp",
    "prop_tool/prop_tool_scene.cpp",
    "prop_tool/prop_tool_light.cpp",
    "prop_tool/prop_tool_mesh.cpp",

    "prop_mesh_utils.cpp",

    "prop_instance.cpp",
    "prop_ess_entity.cpp",
    "prop_mesh_data_instance.cpp",
    "prop_voxelman_light.cpp",
]

if env['tools']:
    sources.append("prop_tool/prop_tool_editor_plugin.cpp")

if ARGUMENTS.get('custom_modules_shared', 'no') == 'yes':
    # Shared lib compilation
    module_env.Append(CCFLAGS=['-fPIC'])
    module_env['LIBS'] = []
    shared_lib = module_env.SharedLibrary(target='#bin/props', source=sources)
    shared_lib_shim = shared_lib[0].name.rsplit('.', 1)[0]
    env.Append(LIBS=[shared_lib_shim])
    env.Append(LIBPATH=['#bin'])
else:
    # Static compilation
    module_env.add_source_files(env.modules_sources, sources)

